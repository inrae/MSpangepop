# Load the configuration file
configfile: ".config/masterconfig.yaml"

import os
import yaml

# Retrieve variables from the config file
container_registry = config.get("container_registry", "docker://registry.forgemia.inra.fr/pangepop/mspangepop")
output_dir = config.get("output_dir", "results/")

# Retrieve memory multiplier from config
memory_multiplier = config.get("memory_multiplier", 1)

def get_input_files():
    inputs = []
    for sample in config["samples"].keys():
        chr_n = config["samples"][sample]["chr_n"]
        chromosomes = [f"{i}" for i in range(1, chr_n + 1)]
        inputs.extend([os.path.join(output_dir, f"{sample}_results", "04_gfa", f"chr_{chromosome}_graph.gfa") for chromosome in chromosomes])
    return inputs

rule all:
    input:
        get_input_files()

rule samtools_index:
    input:
        fasta=lambda wildcards: config["samples"][wildcards.sample]["fasta_gz"]
    output:
        fai=os.path.join(output_dir, "{sample}_results", "01_chromosome_index", "{sample}_full.fai")
    threads: 1
    resources:
        mem_mb=lambda wildcards: int(10000 * memory_multiplier),
        time="10:00:00"
    params: 
        out=os.path.join(output_dir, "{sample}_results", "01_chromosome_index")  
    container:
        f"{container_registry}/samtool:1.21"
    benchmark:
        os.path.join(output_dir, "{sample}_results", "benchmark", "samtools_index_benchmark.txt")
    shell:
        """
        echo 'ðŸ”¹ MSpangepop -> Starting {wildcards.sample} indexing...' && 
        samtools faidx {input.fasta} &&
        mv {input.fasta}.fai {output.fai} && echo 'âœ… MSpangepop -> {wildcards.sample} indexing successful.' &&
        rm {input.fasta}.gzi || true 
        """

rule msprime_simulation:    
    input:
        fai=rules.samtools_index.output
    output:
        os.path.join(output_dir, "{sample}_results", "temp", "chr_{chromosome}_msprime_simulation.json")
    params:
        pop_size=lambda wildcards: config["samples"][wildcards.sample]["population_size"],
        mut_rate=lambda wildcards: config["samples"][wildcards.sample]["mutation_rate"],
        reco_rate=lambda wildcards: config["samples"][wildcards.sample]["recombination_rate"],
        n=lambda wildcards: config["samples"][wildcards.sample]["sample_size"],
        out=lambda wildcards: os.path.join(output_dir, f"{wildcards.sample}_results", "temp"),
        model=lambda wildcards: config["samples"][wildcards.sample]["model"]
    resources:
        mem_mb=lambda wildcards: int(8000 * memory_multiplier),
        time="10:00:00"
    benchmark:
        os.path.join(output_dir, "{sample}_results", "benchmark", "msprime_chr_{chromosome}_benchmark.txt")
    container:
        "./temporary_dependencies/msprime_box.sif"
    shell:
        """
        python3 workflow/scripts/msprime_simulation.py -fai {input.fai} -p {params.pop_size} -mu {params.mut_rate} -r {params.reco_rate} -n {params.n} -o {params.out} -c {wildcards.chromosome} -mo '{params.model}'
        """

rule generate_variant_type:
    input:
        json = rules.msprime_simulation.output
    output:
        os.path.join(output_dir, "{sample}_results", "02_msprime_simulation", "chr_{chromosome}_augmented_ms_sim.json")
    params:
        svg = lambda wildcards: os.path.join(output_dir, f"{wildcards.sample}_results", "temp", f"chr_{wildcards.chromosome}_tree.svg"),
        out = os.path.join(output_dir, "{sample}_results", "02_msprime_simulation"),
        sv_type_file = config.get("sv_type_file")
    resources:
        mem_mb=lambda wildcards: int(8000 * memory_multiplier),
        time="10:00:00"
    container:
        "./temporary_dependencies/msprime_box.sif"
    benchmark:
        os.path.join(output_dir, "{sample}_results", "benchmark", "generate_variant_{chromosome}_benchmark.txt")
    shell:
        """
        python3 workflow/scripts/generate_variant_type.py --json {input.json} --yaml {params.sv_type_file} --output {output} --chromosome {wildcards.chromosome}
        mv {params.svg} {params.out}
        """

rule split_recombination:
    input:
        json = rules.generate_variant_type.output,
        fasta=lambda wildcards: config["samples"][wildcards.sample]["fasta_gz"]
    output:
        os.path.join(output_dir, "{sample}_results", "03_recombination_sequence", "chr_{chromosome}_recombinations.fasta.gz")
    resources:
        mem_mb=lambda wildcards: int(8000 * memory_multiplier),
        time="10:00:00"
    benchmark:
        os.path.join(output_dir, "{sample}_results", "benchmark", "split_recombination_chr_{chromosome}_benchmark.txt")
    container:
        "./temporary_dependencies/msprime_box.sif"
    shell:
        """
        python3 workflow/scripts/split_recombination.py --json {input.json} --fasta {input.fasta} --chromosome {wildcards.chromosome} --output {output} 
        """       
        
rule create_graph:
    input:
        json = rules.generate_variant_type.output,
        splited_fasta=rules.split_recombination.output
    output:
        os.path.join(output_dir, "{sample}_results", "04_gfa", "chr_{chromosome}_graph.gfa")
    resources:
        mem_mb=lambda wildcards: int(8000 * memory_multiplier),
        time="10:00:00"
    benchmark:
        os.path.join(output_dir, "{sample}_results", "benchmark", "create_graph_chr_{chromosome}_benchmark.txt")
    container:
        "./temporary_dependencies/msprime_box.sif"
    shell:
        """
        touch {output}
        python3 workflow/scripts/fasta_to_gfa.py --splited_fasta {input.splited_fasta}
        python3 workflow/scripts/json_preorder_traversal.py --json {input.json} --chromosome {wildcards.chromosome}
        """       