configfile: ".config/masterconfig.yaml"

import os
import yaml

# Retrieve variables from the config file
container_registry = config.get("container_registry", "docker://registry.forgemia.inra.fr/pangepop/mspangepop")
output_dir = config.get("output_dir", "results/")

# Retrieve memory multiplier from config
memory_multiplier = config.get("memory_multiplier", 1)

def get_input_files():
    inputs = []
    for sample in config["samples"].keys():
        chr_n = config["samples"][sample]["chr_n"]
        chromosomes = [f"{i}" for i in range(1, chr_n + 1)]
        inputs.extend([os.path.join(output_dir, f"{sample}", "temp", f"chr_{chromosome}", f"chr_{chromosome}_augmented_traversal.json") for chromosome in chromosomes])
        inputs.extend([os.path.join(output_dir, f"{sample}", "temp", f"chr_{chromosome}", f"chr_{chromosome}_recombinations.fasta.gz") for chromosome in chromosomes])
    return inputs

rule all:
    input:
        get_input_files(),
        expand(os.path.join(output_dir, "{sample}", "{sample}_params_recap.txt"), sample=config["samples"].keys())

rule save_recap:
    input:
        configfile= ".config/masterconfig.yaml"
    output:
        recap_file=os.path.join(output_dir, "{sample}", "{sample}_params_recap.txt")
    params:
        output_dir=os.path.join(output_dir, "{sample}"),
        current_run="{sample}"
    resources:
        mem_mb=lambda wildcards: int(1000),
        time="00:10:00"
    container:
        "./temporary_dependencies/msprime_box.sif"
    shell:
        """
        python3 workflow/scripts/recap.py {input.configfile} {params.output_dir} {params.current_run}
        """

rule samtools_index:
    input:
        fasta=lambda wildcards: config["samples"][wildcards.sample]["fasta_gz"]
    output:
        fai=temp(os.path.join(output_dir, "{sample}", "temp", "{sample}_full.fai"))
    resources:
        mem_mb=lambda wildcards: int(50000 * memory_multiplier),
        time="10:00:00"
    params: 
        out=os.path.join(output_dir, "{sample}", "01_chromosome_index")  
    container:
        f"{container_registry}/samtool:1.21"
    benchmark:
        os.path.join(output_dir, "{sample}", "benchmark", "samtools_index_benchmark.txt")
    shell:
        """
        echo 'ðŸ”¹ MSpangepop -> Starting {wildcards.sample} indexing...' && 
        samtools faidx {input.fasta} && \
            mv {input.fasta}.fai {output.fai} && \
            echo 'âœ… MSpangepop -> {wildcards.sample} indexing successful.'

        if [ -f {input.fasta}.gzi ]; then 
            rm {input.fasta}.gzi
        fi
        """

rule msprime_simulation:    
    input:
        fai=rules.samtools_index.output
    output:
        json = temp(os.path.join(output_dir, "{sample}", "temp", "chr_{chromosome}", "chr_{chromosome}_msprime_simulation.json"))
    params:
        pop_size=lambda wildcards: config["samples"][wildcards.sample]["population_size"],
        mut_rate=lambda wildcards: config["samples"][wildcards.sample]["mutation_rate"],
        reco_rate=lambda wildcards: config["samples"][wildcards.sample]["recombination_rate"],
        n=lambda wildcards: config["samples"][wildcards.sample]["sample_size"],
        out=lambda wildcards: os.path.join(output_dir, f"{wildcards.sample}", "temp"),
        model=lambda wildcards: config["samples"][wildcards.sample]["model"],
        readable_json=lambda wildcards: config["samples"][wildcards.sample].get("readable_json", False), 
        seed=lambda wildcards: config["samples"][wildcards.sample].get("seed", None) 
    resources:
        mem_mb=lambda wildcards: int(200000 * memory_multiplier),
        time="60:00:00"
    benchmark:
        os.path.join(output_dir, "{sample}", "benchmark", "msprime_chr_{chromosome}_benchmark.txt")
    container:
        "./temporary_dependencies/msprime_box.sif"
    shell:
        """
        python3 workflow/scripts/msprime_simulation.py \
            -fai {input.fai} \
            -p {params.pop_size} \
            -mu {params.mut_rate} \
            -r {params.reco_rate} \
            -n {params.n} \
            -o {params.out} \
            -c {wildcards.chromosome} \
            -mo {params.model} \
            --readable_json {params.readable_json} \
            --seed {params.seed}
        """

rule coalescent_traversal:
    input:
        json = rules.msprime_simulation.output.json
    output:
        traversal = temp(os.path.join(output_dir, "{sample}", "temp", "chr_{chromosome}", "chr_{chromosome}_traversal.json"))
    params:
        readable_json=lambda wildcards: config["samples"][wildcards.sample].get("readable_json", False) 
    resources:
        mem_mb=lambda wildcards: int(50000 * memory_multiplier),
        time="10:00:00"
    threads: 10
    benchmark:
        os.path.join(output_dir, "{sample}", "benchmark", "coalescent_traversal_chr_{chromosome}_benchmark.txt")
    container:
        "./temporary_dependencies/msprime_box.sif"
    shell: 
        """
        python3 workflow/scripts/coalescent_traversal.py --json {input.json} --chromosome {wildcards.chromosome} --output_file {output.traversal} --threads {threads} --readable_json {params.readable_json}
        """

rule split_recombination:
    input:
        json = rules.msprime_simulation.output.json,
        fasta=lambda wildcards: config["samples"][wildcards.sample]["fasta_gz"]
    output:
        os.path.join(output_dir, "{sample}", "temp", "chr_{chromosome}", "chr_{chromosome}_recombinations.fasta.gz")
    resources:
        mem_mb=lambda wildcards: int(50000 * memory_multiplier),
        time="10:00:00"
    benchmark:
        os.path.join(output_dir, "{sample}", "benchmark", "split_recombination_chr_{chromosome}_benchmark.txt")
    container:
        "./temporary_dependencies/msprime_box.sif"
    shell:
        """
        python3 workflow/scripts/split_recombination.py --json {input.json} --fasta {input.fasta} --chromosome {wildcards.chromosome} --output {output} 
        """       

rule draw_variants:
    input:
        json = rules.coalescent_traversal.output.traversal
    output:
        os.path.join(output_dir, "{sample}", "temp", "chr_{chromosome}", "chr_{chromosome}_augmented_traversal.json")
    params:
        sv_type_file = config.get("sv_type_file"),
        minimal_variant_size=lambda wildcards: config["samples"][wildcards.sample]["minimal_variant_size"],
        readable_json=lambda wildcards: config["samples"][wildcards.sample].get("readable_json", False) 
    threads: 10
    resources:
        mem_mb=lambda wildcards: int(10000 * memory_multiplier),
        time="10:00:00"
    container:
        "./temporary_dependencies/msprime_box.sif"
    benchmark:
        os.path.join(output_dir, "{sample}", "benchmark", "generate_variant_{chromosome}_benchmark.txt")
    shell:
        """
        python3 workflow/scripts/draw_variants.py --json {input.json} --yaml {params.sv_type_file} --output {output} --chromosome {wildcards.chromosome} --threads {threads} --minimal_variant_size {params.minimal_variant_size} --readable_json {params.readable_json}
        """

rule create_graph:
    input:
        splited_fasta=rules.split_recombination.output,
        traversal = rules.coalescent_traversal.output.traversal,
    output:
        gfa = os.path.join(output_dir, "{sample}", "04_gfa", "chr_{chromosome}_graph.gfa"),
    resources:
        mem_mb=lambda wildcards: int(50000 * memory_multiplier),
        time="10:00:00"
    benchmark:
        os.path.join(output_dir, "{sample}", "benchmark", "create_graph_chr_{chromosome}_benchmark.txt")
    container:
        "./temporary_dependencies/msprime_box.sif"
    threads:4
    shell:
        """
        python3 workflow/scripts/json_to_gfa.py --splited_fasta {input.splited_fasta} --output_file {output.gfa} --sample {wildcards.sample} --chromosome {wildcards.chromosome} 
        """

onsuccess:
    print("âœ… MSpangepop -> Workflow completed successfully")
    shell("date")

onerror: 
    print("âŒ MSpangepop -> Workflow failed, check logs")
    shell("date")
