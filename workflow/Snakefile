configfile: ".config/masterconfig.yaml"

import os
import yaml

# Retrieve variables from the config file
container_registry = config.get("container_registry", "docker://registry.forgemia.inra.fr/pangepop/mspangepop")
output_dir = config.get("output_dir", "results/")

# Retrieve memory multiplier from config
memory_multiplier = config.get("memory_multiplier", 1)

def get_input_files():
    inputs = []
    for sample in config["samples"].keys():
        chr_n = config["samples"][sample]["chr_n"]
        chromosomes = [f"{i}" for i in range(1, chr_n + 1)]
        inputs.extend([os.path.join(output_dir, f"{sample}_results", "temp", f"chr_{chromosome}", f"chr_{chromosome}_msprime_simulation.json") for chromosome in chromosomes])
    return inputs

rule all:
    input:
        get_input_files(),
        expand(os.path.join(output_dir, "{sample}_results", "{sample}_params_recap.txt"), sample=config["samples"].keys())

rule samtools_index:
    input:
        fasta=lambda wildcards: config["samples"][wildcards.sample]["fasta_gz"]
    output:
        fai=temp(os.path.join(output_dir, "{sample}_results", "temp", "{sample}_full.fai"))
    threads: 1
    resources:
        mem_mb=lambda wildcards: int(50000 * memory_multiplier),
        time="10:00:00"
    params: 
        out=os.path.join(output_dir, "{sample}_results", "01_chromosome_index")  
    container:
        f"{container_registry}/samtool:1.21"
    benchmark:
        os.path.join(output_dir, "{sample}_results", "benchmark", "samtools_index_benchmark.txt")
    shell:
        """
        echo 'ðŸ”¹ MSpangepop -> Starting {wildcards.sample} indexing...' && 
        samtools faidx {input.fasta} && \
            mv {input.fasta}.fai {output.fai} && \
            echo 'âœ… MSpangepop -> {wildcards.sample} indexing successful.'

        if [ -f {input.fasta}.gzi ]; then 
            rm {input.fasta}.gzi
        fi
        """

rule msprime_simulation:    
    input:
        fai=rules.samtools_index.output
    output:
        json = temp(os.path.join(output_dir, "{sample}_results", "temp", "chr_{chromosome}", "chr_{chromosome}_msprime_simulation.json")),
        ancestry_svg = temp(os.path.join(output_dir, "{sample}_results", "temp", "chr_{chromosome}", "chr_{chromosome}_ancestry.svg")),
        mutations_svg = temp(os.path.join(output_dir, "{sample}_results", "temp", "chr_{chromosome}", "chr_{chromosome}_mutations.svg"))
    params:
        pop_size=lambda wildcards: config["samples"][wildcards.sample]["population_size"],
        mut_rate=lambda wildcards: config["samples"][wildcards.sample]["mutation_rate"],
        reco_rate=lambda wildcards: config["samples"][wildcards.sample]["recombination_rate"],
        n=lambda wildcards: config["samples"][wildcards.sample]["sample_size"],
        out=lambda wildcards: os.path.join(output_dir, f"{wildcards.sample}_results", "temp"),
        model=lambda wildcards: config["samples"][wildcards.sample]["model"]
    resources:
        mem_mb=lambda wildcards: int(200000 * memory_multiplier),
        time="60:00:00"
    benchmark:
        os.path.join(output_dir, "{sample}_results", "benchmark", "msprime_chr_{chromosome}_benchmark.txt")
    container:
        "./temporary_dependencies/msprime_box.sif"
    shell:
        """
        python3 workflow/scripts/msprime_simulation.py \
            -fai {input.fai} \
            -p {params.pop_size} \
            -mu {params.mut_rate} \
            -r {params.reco_rate} \
            -n {params.n} \
            -o {params.out} \
            -c {wildcards.chromosome} \
            -mo {params.model}
        """

rule draw_variants:
    input:
        json = rules.msprime_simulation.output.json
    output:
        os.path.join(output_dir, "{sample}_results", "01_msprime_simulation", "chr_{chromosome}_augmented_ms_sim.json")
    params:
        sv_type_file = config.get("sv_type_file"),
        minimal_variant_size=lambda wildcards: config["samples"][wildcards.sample]["minimal_variant_size"]
    threads: 10
    resources:
        mem_mb=lambda wildcards: int(10000 * memory_multiplier),
        time="10:00:00"
    container:
        "./temporary_dependencies/msprime_box.sif"
    benchmark:
        os.path.join(output_dir, "{sample}_results", "benchmark", "generate_variant_{chromosome}_benchmark.txt")
    shell:
        """
        python3 workflow/scripts/draw_variants.py --json {input.json} --yaml {params.sv_type_file} --output {output} --chromosome {wildcards.chromosome} --threads {threads} --minimal_variant_size {params.minimal_variant_size}
        """

rule split_recombination:
    input:
        json = rules.msprime_simulation.output.json,
        fasta=lambda wildcards: config["samples"][wildcards.sample]["fasta_gz"]
    output:
        os.path.join(output_dir, "{sample}_results", "temp", "chr_{chromosome}_recombinations.fasta.gz")
    resources:
        mem_mb=lambda wildcards: int(50000 * memory_multiplier),
        time="10:00:00"
    benchmark:
        os.path.join(output_dir, "{sample}_results", "benchmark", "split_recombination_chr_{chromosome}_benchmark.txt")
    container:
        "./temporary_dependencies/msprime_box.sif"
    shell:
        """
        python3 workflow/scripts/split_recombination.py --json {input.json} --fasta {input.fasta} --chromosome {wildcards.chromosome} --output {output} 
        """       

rule coalescent_preorder_traversal:
    input:
        json = rules.draw_variants.output
    output:
        traversal = os.path.join(output_dir, "{sample}_results", "04_gfa", "chr_{chromosome}_traversal.json")
    params:
        readble_json=lambda wildcards: config["samples"][wildcards.sample].get("readble_json", False) 
    resources:
        mem_mb=lambda wildcards: int(50000 * memory_multiplier),
        time="10:00:00"
    threads: 10
    benchmark:
        os.path.join(output_dir, "{sample}_results", "benchmark", "coalescent_preorder_traversal_chr_{chromosome}_benchmark.txt")
    container:
        "./temporary_dependencies/msprime_box.sif"
    shell: 
        """
        python3 workflow/scripts/coalescent_preorder_traversal.py --json {input.json} --chromosome {wildcards.chromosome} --output_file {output.traversal} --threads {threads} --readble_json {params.readble_json}
        """
    
rule create_graph:
    input:
        splited_fasta=rules.split_recombination.output,
        traversal = rules.coalescent_preorder_traversal.output.traversal,
    output:
        gfa = os.path.join(output_dir, "{sample}_results", "04_gfa", "chr_{chromosome}_graph.gfa"),
    resources:
        mem_mb=lambda wildcards: int(50000 * memory_multiplier),
        time="10:00:00"
    benchmark:
        os.path.join(output_dir, "{sample}_results", "benchmark", "create_graph_chr_{chromosome}_benchmark.txt")
    container:
        "./temporary_dependencies/msprime_box.sif"
    threads:4
    shell:
        """
        python3 workflow/scripts/json_to_gfa.py --splited_fasta {input.splited_fasta} --output_file {output.gfa} --sample {wildcards.sample} --chromosome {wildcards.chromosome} 
        """

rule save_recap:
    input:
        configfile= ".config/masterconfig.yaml"
    output:
        recap_file=os.path.join(output_dir, "{sample}_results", "{sample}_params_recap.txt")
    params:
        output_dir=os.path.join(output_dir, "{sample}_results"),
        current_run="{sample}"
    resources:
        mem_mb=lambda wildcards: int(1000),
        time="00:30:00"
    container:
        "./temporary_dependencies/msprime_box.sif"
    shell:
        """
        python3 workflow/scripts/recap.py {input.configfile} {params.output_dir} {params.current_run}
        """

onsuccess:
    print("âœ… MSpangepop -> Workflow completed successfully")
    shell("date")

onerror: 
    print("âŒ MSpangepop -> Workflow failed, check logs")
    shell("date")
